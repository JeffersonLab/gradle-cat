plugins {
    id "java-gradle-plugin"
    id "com.diffplug.spotless" version "7.2.1"
    id "com.vanniktech.maven.publish" version "0.34.0"
}

description = 'Gradle file concatenation plugin'
group = "org.jlab"
version = new File("${projectDir}/VERSION").text.trim()

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

repositories {
    mavenCentral()
}
dependencies {
    testImplementation "junit:junit:4.13"
}
gradlePlugin {
    plugins {
       create("cat") {
            id = "org.jlab.cat"
            displayName = "Gradle file concatenation plugin"
            description = "This plugin is useful to combine / merge files such as web application JavaScript or CSS resources."
            implementationClass = "org.jlab.cat.CatPlugin"
        }
    }
}
test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
        showStandardStreams = true
    }
}
mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()
    coordinates("org.jlab", "cat", version)

    pom {
        name = project.name
        description = project.description
        url = "https://github.com/JeffersonLab/gradle-cat/"
        licenses {
            license {
                name = "The MIT License"
                url = "https://github.com/JeffersonLab/gradle-cat/blob/master/LICENSE"
            }
        }
        developers {
            developer {
                id = "slominskir"
                name = "Ryan Slominski"
                url = "https://github.com/slominskir/"
            }
        }
        scm {
            url = "https://github.com/JeffersonLab/gradle-cat"
            connection = "scm:git:git://github.com/JeffersonLab/gradle-cat.git"
            developerConnection = "scm:git:ssh://git@github.com/JeffersonLab/gradle-cat.git"
        }
    }
}
spotless {
    java {
        googleJavaFormat()
    }
}