plugins {
    id "signing"
    id "com.gradle.plugin-publish" version "1.1.0"
}

description 'Gradle file concatenation plugin'
group = "org.jlab"
version = "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "junit:junit:4.13"
}

pluginBundle {
    website = "https://github.com/jeffersonlab/gradle-cat"
    vcsUrl = "https://github.com/jeffersonlab/gradle-cat"
    tags = ["cat", "concatenate", "merge", "combine"]
}

gradlePlugin {
    plugins {
       create("cat") {
            id = "org.jlab.cat"
            displayName = "Gradle file concatenation plugin"
            description = "This plugin is useful to combine / merge files such as web application JavaScript or CSS resources."
            implementationClass = "org.jlab.cat.CatPlugin"
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

signing {
    sign publishing.publications
}

publishing {
    repositories {
        maven {
            name "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("ossrh.user") ?: System.getenv("OSSRH_USER")
                password = project.findProperty("ossrh.token") ?: System.getenv("OSSRH_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
            pom {
                name = project.name
                description = project.description
                url = "https://github.com/JeffersonLab/gradle-cat"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/JeffersonLab/gradle-cat/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "slominskir"
                        name = "Ryan Slominski"
                        email = "ryans@jlab.org"
                    }
                }
                scm {
                    url = "https://github.com/JeffersonLab/gradle-cat.git"
                }
            }
        }
    }
}